#include <Arduino.h>
#include "ConfigManager.h"
#include <AsyncTCP.h>
#include <ESPAsyncWebServer.h>
AsyncWebServer server(80);

ConfigManagerClass cfg;
ConfigManagerClass::LogCallback ConfigManagerClass::logger = nullptr;

// Basic setting using new aggregate initialization (ConfigOptions<T>)
Config<int> updateInterval(ConfigOptions<int>{
    .keyName = "interval",
    .category = "main",
    .defaultValue = 30,
    .prettyName = "Update Interval (seconds)"
});

// Dynamic visibility example: show static IP fields only if DHCP disabled
struct WiFi_Settings {
  Config<String> wifiSsid;
  Config<String> wifiPassword;
  Config<bool>   useDhcp;
  Config<String> staticIp;
  Config<String> gateway;
  Config<String> subnet;

  WiFi_Settings() :
    wifiSsid(ConfigOptions<String>{ .keyName="ssid", .category="wifi", .defaultValue="MyWiFi", .prettyName="WiFi SSID", .prettyCat="Network Settings" }),
    wifiPassword(ConfigOptions<String>{ .keyName="password", .category="wifi", .defaultValue="secretpass", .prettyName="WiFi Password", .prettyCat="Network Settings", .showInWeb=true, .isPassword=true }),
    useDhcp(ConfigOptions<bool>{ .keyName="dhcp", .category="network", .defaultValue=false, .prettyName="Use DHCP", .prettyCat="Network Settings" }),
    staticIp(ConfigOptions<String>{ .keyName="sIP", .category="network", .defaultValue="192.168.2.126", .prettyName="Static IP", .prettyCat="Network Settings", .showIf=[this](){ return !this->useDhcp.get(); } }),
    gateway(ConfigOptions<String>{ .keyName="GW", .category="network", .defaultValue="192.168.2.250", .prettyName="Gateway", .prettyCat="Network Settings", .showIf=[this](){ return !this->useDhcp.get(); } }),
    subnet(ConfigOptions<String>{ .keyName="subnet", .category="network", .defaultValue="255.255.255.0", .prettyName="Subnet-Mask", .prettyCat="Network Settings", .showIf=[this](){ return !this->useDhcp.get(); } })
  {
    cfg.addSetting(&wifiSsid);
    cfg.addSetting(&wifiPassword);
    cfg.addSetting(&useDhcp);
    cfg.addSetting(&staticIp);
    cfg.addSetting(&gateway);
    cfg.addSetting(&subnet);
  }
};

WiFi_Settings wifiSettings;

void setup() {
  Serial.begin(115200);
  ConfigManagerClass::setLogger([](const char* msg){ Serial.print("[CFG] "); Serial.println(msg); });

  cfg.addSetting(&updateInterval);
  cfg.loadAll();
  cfg.checkSettingsForErrors();

  // Example set a Value from Program
  updateInterval.set(15);
  cfg.saveAll();

  if (wifiSettings.wifiSsid.get().isEmpty()) {
    cfg.startAccessPoint();//start Access Point if no SSID configured
  }

  if (WiFi.getMode() != WIFI_AP) {
    if (wifiSettings.useDhcp.get()) {
      cfg.startWebServer(wifiSettings.wifiSsid.get(), wifiSettings.wifiPassword.get());
    } else {
      // Explicit static (IP, Gateway, Mask)
      cfg.startWebServer(wifiSettings.staticIp.get(), wifiSettings.gateway.get(), wifiSettings.subnet.get(), wifiSettings.wifiSsid.get(), wifiSettings.wifiPassword.get());
    }
  }
  delay(1000);
  if (WiFi.status() == WL_CONNECTED) {
    cfg.setupOTA("Ota-esp32-device", "ota1234");
    cfg.enableWebSocketPush(2000); // periodic push (interval ms)
  }
}

void loop() {
  cfg.handleClient();
  cfg.handleWebsocketPush();
  cfg.handleOTA();
  delay(cfg.updateInterval.get());
}